apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "hk.collaction.timber"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 161310 // 0 = alpha, 1 = beta, 2 = release
        versionName "Ficus Microcarpa 131a"
        multiDexEnabled true
        resConfigs "en", "zh"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            useProguard false
            resValue "string", "GOOGLE_API_KEY", "\"" + getSecretKey("GOOGLE_API_KEY") + "\""
            buildConfigField "String", "API_BASE_URL", "\"" + getSecretKey("API_BASE_URL_DEV") + "\""
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "GOOGLE_API_KEY", "\"" + getSecretKey("GOOGLE_API_KEY") + "\""
            buildConfigField "String", "API_BASE_URL", "\"" + getSecretKey("API_BASE_URL_RELEASE") + "\""
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

static def getSecretKey(name) {
    Properties props = new Properties()
    File f = new File('./../Key/timber.secrets.properties')
    if (f.exists()) {
        props.load(new FileInputStream(f))
        return props[name]
    } else {
        return name
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
}

dependencies {

    // Google Android Support
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v13:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'

    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.0'

    // Placeholder View
    implementation 'com.mindorks:placeholderview:0.7.1'

    // RxBinding Butter Knife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Material Dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.6.1'

    // GSON
    implementation 'com.google.code.gson:gson:2.8.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    // Roboto TextView
    implementation 'com.github.johnkil.android-robototextview:robototextview:4.0.0'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }

    // https://github.com/Blankj/AndroidUtilCode
    implementation 'com.blankj:utilcode:1.16.3'
}

apply plugin: 'com.google.gms.google-services'