plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "hk.collaction.timber"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 211712 // 0 = alpha, 1 = beta, 2 = release
        versionName "Ficus Microcarpa 171"
        resConfigs "en", "zh"
        flavorDimensions "default"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('secrets.properties').newDataInputStream())

        dev {
            resValue "string", "GOOGLE_API_KEY", properties.getProperty("key.google_api_key")
            buildConfigField "String", "API_BASE_URL", properties.getProperty("key.api_base_url_dev")
            buildConfigField "String", "CONTACT_EMAIL", properties.getProperty("key.contact_email")
        }
        prd {
            resValue "string", "GOOGLE_API_KEY", properties.getProperty("key.google_api_key")
            buildConfigField "String", "API_BASE_URL", properties.getProperty("key.api_base_url_release")
            buildConfigField "String", "CONTACT_EMAIL", properties.getProperty("key.contact_email")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
//        https://github.com/Kotlin/kotlinx.coroutines/issues/976
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"]
    }
}

dependencies {
    // Jetbrains
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2'

    // Google Android Support
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.5.0'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.firebase:firebase-core:20.0.2'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.6'
    implementation 'com.google.firebase:firebase-analytics-ktx:20.0.2'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // https://github.com/InsertKoinIO/koin
    implementation "io.insert-koin:koin-android:$koin_version"

    // Placeholder View
    implementation 'com.github.janishar.PlaceHolderView:placeholderview:1.0.3'
    kapt 'com.github.janishar.PlaceHolderView:placeholderview-compiler:1.0.3'

    // https://github.com/coil-kt/coil
    implementation("io.coil-kt:coil:1.4.0")

    // https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // https://github.com/square/okhttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation 'com.github.himphen:logger:3.0.1'
}